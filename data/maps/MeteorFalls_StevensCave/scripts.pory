raw `
.set LOCALID_STEVEN, 4
.set LOCALID_DEVON, 1

MeteorFalls_StevensCave_MapScripts::
	.byte 0

MeteorFalls_StevensCave_EventScript_Steven::
	lock
	goto_if_set FLAG_DEFEATED_METEOR_FALLS_STEVEN, MeteorFalls_StevensCave_EventScript_Defeated
	waitse
	playse SE_PIN
	applymovement LOCALID_STEVEN, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_STEVEN, Common_Movement_Delay48
	waitmovement 0
	applymovement LOCALID_STEVEN, Common_Movement_FacePlayer
	waitmovement 0
	msgbox MeteorFalls_StevensCave_Text_ShouldKnowHowGoodIAmExpectWorst, MSGBOX_DEFAULT
	trainerbattle_no_intro TRAINER_STEVEN, MeteorFalls_StevensCave_Text_StevenDefeat
	msgbox MeteorFalls_StevensCave_Text_MyPredictionCameTrue, MSGBOX_DEFAULT
	setflag FLAG_DEFEATED_METEOR_FALLS_STEVEN
	release
	end

MeteorFalls_StevensCave_EventScript_Defeated::
	applymovement LOCALID_STEVEN, Common_Movement_FacePlayer
	waitmovement 0
	msgbox MeteorFalls_StevensCave_Text_MyPredictionCameTrue, MSGBOX_DEFAULT
	release
	end

MeteorFalls_StevensCave_Text_ShouldKnowHowGoodIAmExpectWorst:
	.string "STEVEN: Oh, wow, {PLAYER}{KUN}.\n"
	.string "I'm amazed you knew where to find me.\p"
	.string "Do you, uh…maybe think of me as\n"
	.string "just a rock maniac?\p"
	.string "No, that can't be right.\p"
	.string "We battled alongside each other at\n"
	.string "the SOOTOPOLIS SPACE CENTER.\p"
	.string "You should have a very good idea\n"
	.string "about how good I am.\p"
	.string "Okay, {PLAYER}{KUN}, if you're going to mount\n"
	.string "a serious challenge, expect the worst!$"

MeteorFalls_StevensCave_Text_StevenDefeat:
	.string "You…\n"
	.string "I had no idea you'd become so strong…$"

MeteorFalls_StevensCave_Text_MyPredictionCameTrue:
	.string "STEVEN: Come to think of it, ever since\n"
	.string "our paths first crossed in GRANITE\l"
	.string "CAVE in DEWFORD, I had this feeling.\p"
	.string "I thought that you would eventually\n"
	.string "become the CHAMPION.\p"
	.string "My predictions usually come true.\p"
	.string "And where will you go from here?\p"
	.string "… … … … … …\n"
	.string "… … … … … …\p"
	.string "Fufufu, even I couldn't tell you that.$"
`

script MeteorFalls_StevensCave_EventScript_Devon {
    call_if_eq(VAR_UNUSED_0x408B, 0, MeteorFalls_StevensCave_EventScript_Devon_Is_Worried)
    call_if_eq(VAR_UNUSED_0x408B, 1, MeteorFalls_StevensCave_EventScript_Devon_NoticedPlayer)
    #call_if_eq(VAR_UNUSED_0x408B, 2, MeteorFalls_StevensCave_EventScript_Devon_FollowingPlayer)
}

script MeteorFalls_StevensCave_EventScript_Devon_Is_Worried {
    # Devon's state of panic before he sees the player.
    lockall
    special(SpawnCameraObject)
    applymovement(localId=OBJ_EVENT_ID_CAMERA, movements=CameraMovement_1)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    msgbox(Devon_Confused_Text1, type=MSGBOX_NPC)
    applymovement(localId=LOCALID_DEVON, movements=MeteorFallsMovement_Devon_Confused_1)
    waitmovement(LOCALID_DEVON)
    msgbox(Devon_Confused_Text2, type=MSGBOX_NPC)
    applymovement(localId=OBJ_EVENT_ID_CAMERA, movements=MeteorFallsMovement_Devon_Confused_2)
    applymovement(localId=LOCALID_DEVON, movements=MeteorFallsMovement_Devon_Confused_2)
    waitmovement(LOCALID_DEVON)
    msgbox(Devon_Confused_Text3, type=MSGBOX_NPC)
    applymovement(localId=LOCALID_DEVON, movements=MeteorFallsMovement_Devon_Confused_3)
    waitmovement(LOCALID_DEVON)
    msgbox(Devon_Confused_Text4, type=MSGBOX_NPC)
    applymovement(localId=LOCALID_DEVON, movements=MeteorFallsMovement_Devon_Confused_4)
    msgbox(Devon_Confused_Text5, type=MSGBOX_NPC)
    applymovement(localId=OBJ_EVENT_ID_CAMERA, movements=ReturnCameraToPlayer1)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    setvar(VAR_UNUSED_0x408B, 1)
    special(RemoveCameraObject)
    releaseall

}

script MeteorFalls_StevensCave_EventScript_Devon_NoticedPlayer {
    # Devon notices the player and asks to join them.
    lockall
    faceplayer
	playse(SE_PIN)
	applymovement(localId=LOCALID_DEVON, movements=Common_Movement_ExclamationMark)
    waitse
    msgbox(Devon_Sees_Player_Text, type=MSGBOX_YESNO)
    if(var(VAR_RESULT) == 0) {
        msgbox(Devon_Question_No, type=MSGBOX_NPC)
    }
    else{
		msgbox(Devon_Question_Yes, type=MSGBOX_NPC)
    }
    playse(SE_PIN)
    applymovement(localId=LOCALID_DEVON, movements=Common_Movement_QuestionMark)
    waitse
    msgbox(Devon_Ask_To_Tag_Along, type=MSGBOX_YESNO)
    while (var(VAR_RESULT) != 1) {
        msgbox(Devon_I_Would_Mind, type=MSGBOX_YESNO)
    }
    msgbox(Devon_Nice_To_Meet_You, type=MSGBOX_NPC)
    lockall
    msgbox(Devon_LetsGo, type=MSGBOX_NPC)
    setfollower(LOCALID_DEVON, FOLLOWER_FLAG_CAN_LEAVE_ROUTE, Devon_Follower_Dialogue, PARTNER_DEVON)
    #setfollower(LOCALID_DEVON, FOLLOWER_FLAG_CAN_LEAVE_ROUTE, 0, PARTNER_DEVON)
    setvar(VAR_UNUSED_0x408B, 2)
    setvar(VAR_BELDUMS_CAVE_STATE, 0)
    setvar(VAR_METEOR_FALLS_STATE, 2)
    setflag(FLAG_MET_DEVON)
    releaseall
}

script MeteorFalls_StevensCave_EventScript_Devon_FollowingPlayer {
    # This is executed whenever player talks to Devon after you let him join you.
    # First check if Devon is already following you. If not, add follower.
    # If he's already following you, then play his dialogue.
    lockall
    applymovement(LOCALID_DEVON, Common_Movement_Delay48)
    msgbox(Devon_LetsGo, type=MSGBOX_NPC)
    setfollower(LOCALID_DEVON, FOLLOWER_FLAG_CAN_LEAVE_ROUTE, Devon_Follower_Dialogue, TRUE)
    
    #checkfollower
    #if (var(VAR_RESULT) != 1) {
    #    setfollower(2, FOLLOWER_FLAG_CAN_LEAVE_ROUTE, Devon_Follower_Dialogue, TRUE)
    #}
    #else {
    #    msgbox(Devon_Luxury)
    #}
    releaseall
}

script MeteorFalls_StevensCave_Devons_Trailmarks {
    lockall
    msgbox(Devon_Mark_Spots_SignText, MSGBOX_DEFAULT)
    releaseall
}

movement CameraMovement_1 {
    walk_diag_northeast * 3
}

movement MeteorFallsMovement_Devon_Confused_1 {
    walk_in_place_left
    walk_in_place_right
}

movement MeteorFallsMovement_Devon_Confused_2 {
    walk_down * 4
}

movement MeteorFallsMovement_Devon_Confused_3 {
    walk_in_place_right
}

movement MeteorFallsMovement_Devon_Confused_4 {
    walk_in_place_left
}

movement MeteorFallsMovement_Devon_Confused_5 {
    face_up
}

movement MeteorFallsMovement_Devon_NoticePlayer_1 {
    faceplayer
}

movement Devon_Goto_Player_1 {
    walk_left * 4
    face_down
}

movement Devon_Goto_Player_2 {
    walk_left * 4
    walk_down * 2
    faceplayer
}

movement ReturnCameraToPlayer1{
    #walk_diag_northeast * 2
    walk_left * 3
    walk_up * 1
}

movement ReturnCameraToPlayer2{
    walk_left * 3
    walk_up * 1
}

text Devon_Mark_Spots_SignText{
    format("There's an indentation in the wall,\n"
    "as if someone is trying to mark their tracks...")
}

text Devon_Confused_Text1{
    format("???: Down to my last Potion...\n"
    "And I don't have any berries left either..."
    "This isn't good...")
}

text Devon_Confused_Text2{
    format("I'm not even sure I remember the way out.")
}

text Devon_Confused_Text3 {
    format("Wasn't it over there?")
}

text Devon_Confused_Text4 {
    format("No, that was a dead-end...")
}

text Devon_Confused_Text5 {
    format("All right... Don't panic." 
    "Stay calm... Don't panic...")
}

text Devon_Sees_Player_Text {
    format("???: Hey!\p"
           "Phew, what a relief!\p"
           "I've been down here the whole day and I haven't seen a single person..."
           "I'm embarrassed to say it, but I think I'm lost. Do you know the way out?")
}


text Devon_Question_Yes {
    format("Oh, wonderful! That's a relief.\p"
           "There's still the problem of actually making it there, though..."
           "I've run out of pretty much all my travelling supplies.")
}

text Devon_Question_No {
    format("Hmm. So it seems we're both lost...\p"
           "Aside from that, I'm not even sure how I'll make it anywhere."
           "I've run out of pretty much all my travelling supplies.")
}

text Devon_Ask_To_Tag_Along {
    format("… … … … … …\p"
    "A Golden Repel?\n"
    "Huh, I've never heard of that...\l"
    "And you're saying it can repel any wild POKeMON? That's mind-boggling!\p"
    "Would it be all right if I tagged along with you?")
}

text Devon_I_Would_Mind {
    format("Oh, but please! I don't think I can make it out on my own like this... I promise I won't be a burden!\n"
    "Could I please come with you?")
}

text Devon_Nice_To_Meet_You {
    format("Thank you so much!\n"
    "By the way, my name's DEVON STONE.\p" 
    "I'm an ore miner by profession."
    "My POKeMON here is BELDUM, but we're more like travelling companions than battle partners.\p"
    "And you?\p"
    "… … … … … …\p"
    "Ah, so you're {PLAYER}. Nice to meet you!")
}

text Devon_LetsGo {
    format("DEVON: All right, I'm ready! I'll be right behind you.\p"
            "I guess that GOLDEN REPEL will protect us from all wild POKeMON."
            "I don't think there are any trainers here, but if we run into any, I'll do my best to help.")
}

text Devon_Luxury{
    format("DEVON: I quit my job a few months ago, so I'm not sure if I can still call myself an ore miner...\p"
    "I just needed to take some time off to think.\p" 
    "Heh... I guess in such a fast-moving world, that's becoming a bit of a luxury.")
}

text Devon_Luxury_2{
    format("DEVON: So, are you from RUSTBORO, {PLAYER}?\p"
    "… … … … … …\p"
    "That's all right, I understand you don't want to give too many details. We just met, after all.")
}

text Devon_Surfing_1{
    format("DEVON: We're surfing! We're actually surfing!")
}

text Devon_Surfing_2{
    format("DEVON: I probably shouldn't have climbed that waterfall...\p"
    "I'd have no idea how to get down by myself."
    )
}

text Devon_LetsGoToExit {
    format("DEVON: Let's go!")
}

text Devon_NotGoingBackDown {
    format("DEVON: We're not going down there if there are people chasing you. Let's find another path.")
}

script Devon_Follower_Dialogue{
    facefollower
    if (flag(FLAG_FLOATYBALLOON_IS_ACTIVE) == TRUE) {
        random(2)
        if (var(VAR_RESULT) == 1) {
            msgbox(Devon_Surfing_1, type=MSGBOX_NPC)
        }
        if (var(VAR_RESULT) == 2) {
            msgbox(Devon_Surfing_2, type=MSGBOX_NPC)
        }
    }
    else {
        if (var(VAR_METEOR_FALLS_STATE) == 1 || var(VAR_METEOR_FALLS_STATE) == 2) {
            random(2)
            if (var(VAR_RESULT) == 1) {
                msgbox(Devon_Luxury, type=MSGBOX_NPC)
            }
            if (var(VAR_RESULT) == 2) {
                msgbox(Devon_Luxury_2, type=MSGBOX_NPC)
            }
        }
        else {
            if (var(VAR_METEOR_FALLS_STATE) == 3) {
                msgbox(Devon_LetsGoToExit, type=MSGBOX_NPC)
            }
            else {
                if (var(VAR_METEOR_FALLS_STATE) == 4) {
                    msgbox(Devon_NotGoingBackDown, type=MSGBOX_NPC)
                }
            }
        }
    }
}
